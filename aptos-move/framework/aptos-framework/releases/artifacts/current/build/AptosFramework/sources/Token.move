/// This module provides the foundation for (collectible) Tokens often called NFTs
module AptosFramework::Token {
    use Std::ASCII;
    use Std::GUID::{Self, ID};
    use Std::Option::{Self, Option};
    use Std::Signer;
    use Std::Vector;
    use AptosFramework::Table::{Self, Table};

    // Error map
    const EINSUFFICIENT_BALANCE: u64 = 0;
    const EMISSING_CLAIMED_TOKEN: u64 = 1;

    // A creator may publish multiple collections
    struct Collections<TokenType: copy + drop + store> has key {
        collections: Table<ID, Collection<TokenType>>,
    }

    public fun initialize_collections<TokenType: copy + drop + store>(signer: &signer) {
        move_to(
            signer,
            Collections {
                collections: Table::create<ID, Collection<TokenType>>(),
            },
        )
    }

    // The source of Tokens, their collection!
    struct Collection<TokenType: copy + drop + store> has drop, store {
        // Keep track of all Tokens, even if their balance is 0.
        tokens: Table<ID, Token<TokenType>>,
        // Keep track of where all Tokens currently are
        claimed_tokens: Table<ID, vector<address>>,
        // Unique identifier for this collection
        id: ID,
        description: ASCII::String,
        name: ASCII::String,
        // URL for additional information /media
        uri: ASCII::String,
        // Total number of distinct Tokens tracked by the Table
        count: u64,
        // Optional maximum number of tokens allowed within this collections
        maximum: Option<u64>,
    }

    public fun create_collection<TokenType: copy + drop + store>(
        account: &signer,
        description: ASCII::String,
        name: ASCII::String,
        uri: ASCII::String,
        maximum: Option<u64>,
    ): ID acquires Collections {
        let account_addr = Signer::address_of(account);
        let collections = &mut borrow_global_mut<Collections<TokenType>>(account_addr).collections;

        let collection = Collection<TokenType> {
            tokens: Table::create(),
            claimed_tokens: Table::create(),
            id: GUID::id(&GUID::create(account)),
            description,
            name,
            uri,
            count: 0,
            maximum,
        };

        let id = *&collection.id;
        Table::insert(collections, *&id, collection);
        id
    }

    // An accounts set of Tokens
    struct Gallery<TokenType: copy + drop + store> has key {
        gallery: Table<ID, Token<TokenType>>,
    }

    public fun initialize_gallery<TokenType: copy + drop + store>(signer: &signer) {
        move_to(
            signer,
            Gallery {
                gallery: Table::create<ID, Token<TokenType>>(),
            },
        )
    }

		// A non-fungible or semi-fungible (edition) token
		struct Token<TokenType: copy + drop + store> has drop, store {
				// Unique identifier for this token
				id: ID,
        // The collection or set of related Tokens
        collection: ID,
				// Current store of data at this location
				balance: u64,
				// Token data, left as optional as it can be stored directly with the Token or at the source,
        // currently the intent is to copy
				data: Option<TokenData<TokenType>>,
		}

		// Specific data of a token that can be generalized across an entire edition of an Token
		struct TokenData<TokenType: copy + drop + store> has copy, drop, store {
				// Describes this Token
				description: ASCII::String,
				// Additional data that describes this Token
				metadata: TokenType,
				// The name of this Token
				name: ASCII::String,
				// Total number of editions of this Token
				supply: u64,
				/// URL for additional information / media
				uri: ASCII::String,
		}

    // Create a new token, place the metadata into the collection and the token into the gallery
    public fun create_token<TokenType: copy + drop + store>(
        account: &signer,
        collection_id: ID,
        description: ASCII::String,
        name: ASCII::String,
        supply: u64,
        uri: ASCII::String,
        metadata: TokenType,
    ): ID acquires Collections, Gallery {
        let account_addr = Signer::address_of(account);
        let collections = &mut borrow_global_mut<Collections<TokenType>>(account_addr).collections;
        let gallery = &mut borrow_global_mut<Gallery<TokenType>>(account_addr).gallery;

        let some_data = Option::some(TokenData {
            description,
            metadata,
            name,
            supply,
            uri,
        });

        let (collection_data, gallery_data) = if (supply == 1) {
            (Option::none(), some_data)
        } else {
            (some_data, Option::none())
        };

        let collection_token = Token {
            id: GUID::id(&GUID::create(account)),
            collection: *&collection_id,
            balance: supply,
            data: collection_data,
        };

        let token_id  = *&collection_token.id;
        let collection = Table::borrow_mut(collections, &collection_id);
        let claimed_tokens = Vector::empty();
        Vector::push_back(&mut claimed_tokens, account_addr);
        Table::insert(&mut collection.claimed_tokens, *&collection_token.id, claimed_tokens);
        Table::insert(&mut collection.tokens, *&collection_token.id, collection_token);

        let gallery_token = Token {
            id: *&token_id,
            collection: collection_id,
            balance: supply,
            data: gallery_data,
        };

        Table::insert(gallery, *&gallery_token.id, gallery_token);
        token_id
    }

    public fun withdraw_token<TokenType: copy + drop + store>(
        account: &signer,
        token_id: ID,
        amount: u64,
    ): Token<TokenType> acquires Collections, Gallery {
        let account_addr = Signer::address_of(account);

        let gallery = &mut borrow_global_mut<Gallery<TokenType>>(account_addr).gallery;
        let balance = Table::borrow(gallery, &token_id).balance;
        assert!(balance >= amount, EINSUFFICIENT_BALANCE);

        let collection_id = Table::borrow(gallery, &token_id).collection;
        let creator_addr = GUID::id_creator_address(&collection_id);
        let collections = &mut borrow_global_mut<Collections<TokenType>>(creator_addr).collections;
        let collection = Table::borrow_mut(collections, &collection_id);
        let claimed_tokens = Table::borrow_mut(&mut collection.claimed_tokens, &token_id);

        if (balance == amount) {
            let (found, idx) = Vector::index_of(claimed_tokens, &account_addr);
            assert!(found, EMISSING_CLAIMED_TOKEN);
            Vector::swap_remove(claimed_tokens, idx);
            Table::remove(gallery, &token_id)
        } else {
            let token = Table::borrow_mut(gallery, &token_id);
            token.balance = balance - amount;
            Token {
                id: *&token.id,
                collection: *&token.collection,
                balance: amount,
                data: *&token.data,
            }
        }
    }

    public fun deposit_token<TokenType: copy + drop + store>(
        account: &signer,
        token: Token<TokenType>,
    ) acquires Collections, Gallery {
        let account_addr = Signer::address_of(account);

        let creator_addr = GUID::id_creator_address(&token.collection);
        let collections = &mut borrow_global_mut<Collections<TokenType>>(creator_addr).collections;
        let collection = Table::borrow_mut(collections, &token.collection);
        let claimed_tokens = Table::borrow_mut(&mut collection.claimed_tokens, &token.id);
        Vector::push_back(claimed_tokens, account_addr);

        let gallery = &mut borrow_global_mut<Gallery<TokenType>>(account_addr).gallery;
        if (Table::contains_key(gallery, &token.id)) {
            let current_token = Table::borrow_mut(gallery, &token.id);
            current_token.balance = current_token.balance + token.balance
        } else {
            Table::insert(gallery, *&token.id, token)
        }
    }

    #[test(creator = @0x1, owner = @0x2)]
    public fun create_withdraw_deposit_nft(
        creator: signer,
        owner: signer,
    ) acquires Collections, Gallery {
        initialize_collections<u64>(&creator);
        initialize_gallery<u64>(&creator);
        let collection_id = create_collection<u64>(
            &creator,
            ASCII::string(b"Collection: Hello, World"),
            ASCII::string(b"Hello, World"),
            ASCII::string(b"https://aptos.dev"),
            Option::none(),
        );
        let token_id = create_token<u64>(
            &creator,
            collection_id,
            ASCII::string(b"Token: Hello, Token"),
            ASCII::string(b"Hello, Token"),
            1,
            ASCII::string(b"https://aptos.dev"),
            0,
        );

        initialize_gallery<u64>(&owner);
        let token = withdraw_token<u64>(&creator, token_id, 1);
        deposit_token<u64>(&owner, token);
    }
}
